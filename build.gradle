apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'eclipse'

def generateSourceDir = new File("${project.buildDir}/gen-src/main/java")

version             = '2.1-SNAPSHOT'
sourceCompatibility = 1.7
targetCompatibility = 1.7
mainClassName       = "com.jramoyo.qfixmessenger.QFixMessenger"

repositories {
    mavenCentral()
}

configurations {
    xjc
}

dependencies {
    xjc         'com.sun.xml.bind:jaxb-xjc:2.2.7'
    compile     'com.sun.xml.bind:jaxb-impl:2.2.7'
    compile     'jdom:jdom:1.1'
    compile     'log4j:log4j:1.2.17'
    compile     'org.apache.mina:mina-core:1.1.7'
    compile     'org.slf4j:slf4j-api:1.7.7'
    compile     'org.slf4j:slf4j-log4j12:1.7.7'
    testCompile 'junit:junit:4.11'
    testCompile 'org.hamcrest:hamcrest-core:1.3'

    compile     fileTree(dir: 'lib/compile', include: '*.jar')
}

sourceSets {
    main {
        java {
            srcDir generateSourceDir
        }
    }
}

task createDirs () {
    generateSourceDir.mkdirs();
}

task xjc(dependsOn: createDirs) {
    ant.taskdef(
        name:      'xjc',
        classname: 'com.sun.tools.xjc.XJCTask',
        classpath: configurations.xjc.asPath
    )

    ant.xjc(
        destdir:   generateSourceDir,
        extension: true,
        readonly:  true,
    ) {
        schema(
            dir: 'src/main/xsd',
            includes: '*.xsd'
        )
    }
}

task generateSource (dependsOn: xjc) {
}

clean.doLast {
    ant.delete(dir: 'log')
    ant.delete(dir: 'data')
}

cleanEclipse.doLast {
    ant.delete(dir: 'bin')
}

compileJava.dependsOn generateSource

jar {
    manifest {
        attributes 'Implementation-Title': 'QuickFIX Messenger', 'Implementation-Version': version
    }
}

applicationDistribution.from('src/main/cfg') {
    into 'cfg'
}

applicationDistribution.from('src/main/cfg/log4j.properties') {
    into 'lib/log4j'
}

applicationDistribution.from('src/main/dictionary') {
    into 'lib/dictionary'
}

applicationDistribution.from('src/main/scripts') {
    into '.'
}

startScripts.classpath.add(files('$APP_HOME/dictionary'))

startScripts.classpath.add(files('$APP_HOME/log4j'))
